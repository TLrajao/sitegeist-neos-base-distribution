prototype(Sitegeist.Site.Placeholder:Document.Base) < prototype(Neos.Fusion:Component) {
    body = null
    jsonLd = null

    renderer = Neos.Neos:Page {
        head {
            metaViewport = '<meta name="viewport" content="width=device-width">'
            stylesheets = Sitegeist.Site.Placeholder:Resource.StyleSheets

            titleTag {
                content.@process.environmentPrefix {
                    expression = ${Configuration.setting('Sitegeist.Site.Placeholder.environmentPrefix') + ' ' + value}
                    @if.isSet = ${!String.isBlank(Configuration.setting('Sitegeist.Site.Placeholder.environmentPrefix'))}
                }
            }
            jsonLd = afx`<Neos.Fusion:Loop items={props.jsonLd}><script type="application/ld+json">{Json.stringify(item)}</script></Neos.Fusion:Loop>`
        }

        body = ${props.body}

        bodyScripts = Sitegeist.Site.Placeholder:Resource.BodyScripts
        bodyScripts.@position = 'before closingBodyTag'
    }
}
